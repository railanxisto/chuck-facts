apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "br.com.railanxisto.chuckfacts"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "br.com.railanxisto.chuckfacts.MockRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        pickFirst 'mockito-extensions/org.mockito.plugins.MockMaker'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}

preBuild.dependsOn(ktlint)

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.android.support:appcompat-v7:$support"
    implementation "com.android.support:design:$support"
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.legacy:legacy-support-v4:$legacy"
    testImplementation "junit:junit:$junit"

    // Api & Http
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit"

    // Koin
    implementation "org.koin:koin-android:$koin"
    implementation "org.koin:koin-android-viewmodel:$koin"
    testImplementation "org.koin:koin-test:$koin"

    // rxjava
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    // flexbox
    implementation "com.google.android:flexbox:$flexbox"

    // room
    implementation "androidx.room:room-runtime:$room"
    kapt "androidx.room:room-compiler:$room"
    implementation "androidx.room:room-rxjava2:$room"

    // ktlint
    ktlint "com.github.shyiko:ktlint:$ktlint_version"

    // mockito
    testImplementation "org.mockito:mockito-core:$mockito"
    androidTestImplementation "org.mockito:mockito-android:$mockito"

    // tests
    testImplementation "org.robolectric:robolectric:$roboletric"
    testImplementation "androidx.arch.core:core-testing:$core_testing"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidx_espresso"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$androidx_espresso"

    androidTestImplementation "androidx.test.ext:junit:$junit"
    androidTestImplementation "androidx.arch.core:core-testing:$core_testing"
    androidTestImplementation "androidx.test:runner:$test_runner"
    androidTestImplementation "org.koin:koin-test:$koin"
}

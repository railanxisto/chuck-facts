apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "br.com.railanxisto.chuckfacts"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "br.com.railanxisto.chuckfacts.MockRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        pickFirst 'mockito-extensions/org.mockito.plugins.MockMaker'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}

preBuild.dependsOn(ktlint)

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}


dependencies {
    implementation project(':domain')
    implementation project(':data')
    implementation Dependencies.kotlinStdlib
    implementation Dependencies.compat
    implementation Dependencies.supportCompat
    implementation Dependencies.supportDesign
    implementation Dependencies.coreKtx

    implementation Dependencies.constraintLayout
    implementation Dependencies.legacy
    testImplementation Dependencies.junit

    // Api & Http
    implementation Dependencies.retrofit
    implementation Dependencies.retrofitConverter
    implementation Dependencies.loggingInterceptor
    implementation Dependencies.retrofitRxjava

    // Koin
    implementation Dependencies.koin
    implementation Dependencies.koinViewmodel
    testImplementation Dependencies.koinTest

    // rxjava
    implementation Dependencies.rxJava
    implementation Dependencies.rxAndroid

    // flexbox
    implementation Dependencies.flexbox

    // room
    implementation Dependencies.room
    kapt Dependencies.kaptRoom
    implementation Dependencies.roomRxjava

    // ktlint
    ktlint Dependencies.ktlint

    // mockito
    testImplementation Dependencies.mockitoCore
    androidTestImplementation Dependencies.mockitoAndroid

    // tests
    testImplementation Dependencies.roboletric
    testImplementation Dependencies.coreTesting
    androidTestImplementation Dependencies.espresso
    androidTestImplementation Dependencies.espressoContrib

    androidTestImplementation Dependencies.junitExt
    androidTestImplementation Dependencies.coreTesting
    androidTestImplementation Dependencies.testRunner
    androidTestImplementation Dependencies.koinTest
}
